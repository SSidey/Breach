class_name SkirmishSquad
extends RefCounted
## A wave in the field (specs/22-formation-feel-test.md, Decision 40): its units keep
## their formation places (rank, column) behind the squad's front distance, it moves as a
## block at its slowest unit's speed, and takes orders as a whole. Only the foremost unit
## of each column fights; when one falls the ranks behind step up (compact()).

enum State { MOVING, HOLDING, FIGHTING, ARRIVED, DESTROYED, ROUTING }

const SkirmishUnit = preload("res://sim/skirmish/skirmish_unit.gd")
const MapLayoutDef = preload("res://content/definitions/map_layout_def.gd")
const FormationRoute = preload("res://sim/skirmish/formation/formation_route.gd")
const SquadFrame = preload("res://sim/skirmish/formation/squad_frame.gd")
const FormationCommand = preload("res://sim/skirmish/formation/formation_command.gd")
const PlaceOrder = preload("res://sim/skirmish/formation/place_order.gd")

## Tiles between one rank and the next: one cell (Decisions 48 and 68).
const RANK_DEPTH := 1.0 / MapLayoutDef.CELLS_PER_TILE

var id: int
var faction_id: String
## Which way the squad's front points along its route: +1 up it, -1 down it. An
## about-face flips it.
var direction: int
var home_distance: float
## Tiles along the route; setting it moves `position` with it.
var front_distance: float:
	set(value):
		front_distance = value
		_place()
## The route the squad follows (Decision 75); null is a straight lane along x.
var route: FormationRoute = null:
	set(value):
		route = value
		_place()
## Which way the squad's frame faces (Decision 105): a heading in degrees, clockwise from
## north (90 east), as a unit's bearing; its places are laid out in that turned frame.
var heading := 90.0
## The facing (SquadFrame) nearest its heading, for what still reckons in four ways (an
## exact diagonal rounds clockwise); setting it turns the frame to that way.
var facing: int:
	get:
		return posmod(roundi(heading / 90.0), 4)
	set(value):
		heading = posmod(value, 4) * 90.0
## The centre of its front edge in cells.
var position := Vector2.ZERO
var width: int
var order: SkirmishUnit.Order = SkirmishUnit.Order.ADVANCE
var state: State = State.MOVING
## The squad this one is fighting with its front; 0 = none.
var engaged_with: int = 0
## Hostile squads fighting it on its other edges (Decision 78): SquadEdges edge ->
## {"foe": squad id, "since": tick the contact began}.
var flank_contacts := {}
## The scrum (FormationScrum, Decision 88): its units' places while it fights (unit id ->
## {"at", "next", "goal"}), the line it re-faced to {"anchor", "heading"}, the tick its fight
## began (-1: none) and ticks with nobody able to strike.
var loose := {}
var stance := {}
var fight_since := -1
## Whether it has fought since it set out: groups of different commands form up only
## after both have (FormationGroups).
var fought := false
## Its units out taking the downed (FormationTaking): unit id -> the body; the tick they
## set out (-1: none), and the tick it gave up on them (-1: none) - it then marches on.
var taking := {}
var taking_since := -1
var took_until := -1
## Group pathfinding (FormationPathing): how many of its command's route patches its route
## carries, and where along it (cells) it last looked ahead.
var patched_count := 0
var looked_at := -INF
## Pursuit (ScrumPursuit, Decision 109): whether it may pursue a retreating enemy (false:
## ordered not to), and its units out chasing one on their own (unit id -> {"unit", "foe",
## "from", "leash"}).
var pursues: bool:
	get:
		return command.pursues
	set(value):
		command.pursues = value
var chasers := {}
## A pursuit under way (FormationPursuit): the enemy, the post it left, and how it held it.
var pursuit := {}
var stall_ticks := 0
## What it is doing (FormationManoeuvre.Kind, Decisions 94 and 99): 0 combat, 1
## withdrawing, 2 its route, 3 re-forming, 4 the player's order; it marches only on 4.
var manoeuvre := 4
## A withdrawal under way (FormationWithdraw): ticks it has felt safe; empty when none.
var withdraw := {}
## A hold-until order (FormationStaging, Decision 87); empty when it has none.
var staging := {}
## Its will to fight, 0 to 100 (FormationMorale, Decision 82); -1 until first read.
var morale := -1
## Routing (FormationRout): each fleeing unit's place, and ticks with no enemy near.
var fleeing := {}
var rally_ticks := 0
## Its command's orders (FormationCommand): hurrying and tending its own downed.
var hurry: bool:
	get:
		return command.hurry
	set(value):
		command.hurry = value
var tends: String:
	get:
		return command.tends
	set(value):
		command.tends = value
## Halted by ground it can't cross (FormationMarch.pace); reported once.
var blocked := false
## Narrowed through a gap (FormationNarrowing): its painted places (unit id -> [rank,
## column]), width and shift, and ticks left re-forming.
var painted := {}
var painted_width := 0
var painted_shift := 0.0
var narrow_ticks := 0
var wait_ticks: int = 0
## Re-forming after a reinforcement, and the swaps under way (Decision 46, FormationShuffle):
## [[mover, passed units, ticks left, ticks in all], ...].
var reforming := false
var swaps := []
var units: Array[SkirmishUnit] = []
## The lane's combat width: how wide joined units may spread the line in a fight (Decision
## 51); 0 keeps them within the squad's own columns.
var combat_width := 0
## Units that joined as reinforcements: only they spread beyond the painted columns.
var joined: Array[SkirmishUnit] = []
## Whether this wave merges into a friendly squad it catches up with on the march.
var merges: bool:
	get:
		return command.merges
	set(value):
		command.merges = value
## The command this group of units follows (spec 30 round 3).
var command: FormationCommand
## How far the squad's columns sit off the lane's centre, so widening on one side moves no
## one on screen.
var centre_shift := 0.0


func _init(
	squad_id: int,
	faction: String,
	travel_direction: int,
	home: float,
	formation_width: int,
	members: Array[SkirmishUnit] = [],
	orders: FormationCommand = null
) -> void:
	command = orders if orders != null else FormationCommand.new(home)
	id = squad_id
	faction_id = faction
	direction = travel_direction
	facing = SquadFrame.EAST if direction > 0 else SquadFrame.WEST
	home_distance = home
	front_distance = home
	width = maxi(formation_width, 1)
	for unit in members:
		FormationCommand.enlist(self, unit)


func living() -> Array[SkirmishUnit]:
	return units.filter(func(u): return u.is_alive())


func is_destroyed() -> bool:
	return living().is_empty()


func speed() -> float:
	var slowest := INF
	for unit in living():
		slowest = minf(slowest, unit.speed)
	return 0.0 if slowest == INF else slowest


## Where a unit stands on the route: its rank's depth behind the squad's front.
func unit_distance(unit: SkirmishUnit) -> float:
	return front_distance - direction * unit.rank * RANK_DEPTH


## The unit's columns as a lateral span along `axis` (its squad's lateral axis if none):
## turned from its (rank, column), never mirrored (Decision 74). On a straight lane both
## sides share the lane's lateral axis, centred on it (less any centre_shift).
func lateral_span(unit: SkirmishUnit, axis := Vector2.ZERO) -> Vector2:
	var along := SquadFrame.lateral_axis(heading) if axis == Vector2.ZERO else axis
	var points := SquadFrame.corners(position, heading, width, centre_shift, unit)
	return SquadFrame.extent(points, along)


## The front rank, left to right: only it fights in melee (Decision 47). A column whose
## front cell is empty has no fighter, so the enemy facing it wraps onto the flank.
func fighters() -> Array[SkirmishUnit]:
	var out: Array[SkirmishUnit] = living().filter(func(u): return u.rank == 0)
	out.sort_custom(func(a, b): return a.column < b.column)
	return out


## Step-up: living units move forward a rank while their whole footprint would be clear,
## until nothing more can move - but only front-preferring units step into the front rank
## (Decision 47). If the whole front has fallen, the squad re-anchors: its foremost rank
## becomes rank 0 and front_distance moves back to match, so nobody moves on the route.
## Returns the units that stepped up.
func compact() -> Array[SkirmishUnit]:
	var moved: Array[SkirmishUnit] = []
	var stepped := {}  # unit -> true: those in `moved`
	var cells := _cells()  # who stands where, kept as units step up
	var heading := _headed_for()  # mid-move: the places moves under way are heading for
	var any := true
	while any:
		any = false
		var order_by_place := PlaceOrder.front_first(living())
		for unit: SkirmishUnit in order_by_place:
			if heading["movers"].has(unit):
				continue  # mid-move: it takes the place its move is heading for
			var into_front_ok := unit.rank > 1 or unit.preferred_position == 0
			if unit.rank > 0 and into_front_ok and _clear_ahead(unit, cells, heading["cells"]):
				_step_up(unit, cells)
				any = true
				if not stepped.has(unit):
					stepped[unit] = true
					moved.append(unit)
	var remaining := living()
	if not remaining.is_empty() and not remaining.any(func(u): return u.rank == 0):
		var shift: int = remaining.reduce(func(least, u): return mini(least, u.rank), 1 << 30)
		for unit in remaining:
			unit.rank -= shift
		front_distance -= direction * shift * RANK_DEPTH
	return moved


## {"movers": {unit: true} for the units moving in swaps under way, "cells": {Vector2i(rank,
## column): [mover, ...]} for the places their moves are heading for}.
func _headed_for() -> Dictionary:
	var movers := {}
	var cells := {}
	for swap in swaps:
		for mover in swap["to"]:
			movers[mover] = true
			var to: Vector2i = swap["to"][mover]
			for rank in range(to.x, to.x + mover.footprint_depth):
				for column in range(to.y, to.y + mover.footprint_width):
					if not cells.has(Vector2i(rank, column)):
						cells[Vector2i(rank, column)] = []
					cells[Vector2i(rank, column)].append(mover)
	return {"movers": movers, "cells": cells}


## {Vector2i(rank, column): [unit, ...]}: the places the living units' footprints cover.
func _cells() -> Dictionary:
	var cells := {}
	for unit in living():
		for rank in range(unit.rank, unit.rank + unit.footprint_depth):
			for column in range(unit.column, unit.column + unit.footprint_width):
				if not cells.has(Vector2i(rank, column)):
					cells[Vector2i(rank, column)] = []
				cells[Vector2i(rank, column)].append(unit)
	return cells


## The unit steps up a rank, and `cells` (_cells) with it.
func _step_up(unit: SkirmishUnit, cells: Dictionary) -> void:
	for column in range(unit.column, unit.column + unit.footprint_width):
		cells[Vector2i(unit.rank + unit.footprint_depth - 1, column)].erase(unit)
		if not cells.has(Vector2i(unit.rank - 1, column)):
			cells[Vector2i(unit.rank - 1, column)] = []
		cells[Vector2i(unit.rank - 1, column)].append(unit)
	unit.rank -= 1


## True if no other living unit (`cells`: _cells) stands in the row ahead of the unit across
## its columns, nor is a move under way heading there (`heading`: _headed_for's cells).
func _clear_ahead(unit: SkirmishUnit, cells: Dictionary, heading: Dictionary) -> bool:
	var row := unit.rank - 1
	for column in range(unit.column, unit.column + unit.footprint_width):
		for other in cells.get(Vector2i(row, column), []):
			if other != unit:
				return false
		for mover in heading.get(Vector2i(row, column), []):
			if mover != unit:
				return false
	return true


func _place() -> void:
	var cells := front_distance * MapLayoutDef.CELLS_PER_TILE
	position = route.point_at(cells) if route != null else Vector2(cells, 0.0)
